#!/usr/bin/env zsh

set -e
set -u
set -o pipefail

cd $(dirname $0)

if [[ ! -d $1 || ! ( $2 =~ '^[0-9]+$' ) ]]; then
  echo >&2 "Usage: $0 <year> <day> <exercise>"
  exit 2
fi

TIMEFMT=$'\n%U user %S system %P cpu %*E total'

year=$1
day=$(printf '%02d' $2)
exercise=$3

shift
shift
shift

input="${year}/AOC_${day}.input"
output_directory="build/${year}"
if [[ -n $exercise ]]; then
  name="AOC_${day}_${exercise}"
else
  name="AOC_${day}"
fi

mkdir -p $output_directory

if [[ -e "${year}/${name}.hs" ]]; then
  executable="${output_directory}/${name}"
  stack ghc -- \
    -O2 \
    -o $executable \
    -odir $output_directory -osuf "${name}.o" \
    -hidir $output_directory -hisuf "${name}.hi" \
    "${year}/${name}.hs"
elif [[ -e "${year}/${name}.pl" ]]; then
  executable=(swipl "${year}/${name}.pl")
elif [[ -e "${year}/${name}.swift" ]]; then
  files=("${year}/${name}.swift" ${year}/Helpers/*.swift)
  executable="${output_directory}/${name}"
  swiftformat $files
  swiftc -o $executable $files
  echo 'Compiled.'
  echo
else
  echo "The program does not exist."
  exit 1
fi

if [[ -t 0 ]]; then
  if [[ ! -e $input ]]; then
    echo "${input} does not exist."
    exit 1
  fi

  time $executable "$@" < $input
else
  time $executable "$@"
fi
